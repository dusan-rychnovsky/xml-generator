options {
	STATIC = false;
}

PARSER_BEGIN(DTDParser)
	package cz.dusanrychnovsky.xmlgenerator;

	import cz.dusanrychnovsky.util.regexp.*;
	import cz.dusanrychnovsky.xmlgenerator.schema.graph.*;
	import cz.dusanrychnovsky.xmlgenerator.schema.graph.builder.*;

	import org.apache.commons.io.FileUtils;
	import java.io.IOException;
	import java.io.File;
	import java.io.FileReader;
	import java.io.StringReader;
	import java.util.List;
	import java.util.ArrayList;
	import java.util.Set;
	import java.util.HashSet;
	
	public class DTDParser extends SchemaParser
	{
		private final SchemaGraphBuilder builder = new SchemaGraphBuilder();
		private final int maxCard = 3;
		
		public DTDParser() {
			this(new StringReader(""));
		}
		
		@Override
		public SchemaGraph parse(File schemaFile) throws IOException, ParseException {
			
			ReInit(new FileReader(schemaFile));
			return Schema();
		}
	}
PARSER_END(DTDParser)

SKIP : { " " }
SKIP : { "\t" }
SKIP : { "\n" | "\r" | "\r\n" }

TOKEN : { < EL_DECL_START : "<!ELEMENT" > }
TOKEN : { < AT_DECL_START : "<!ATTLIST" > }
TOKEN : { < DECL_END : ">" > }

TOKEN : { < EMPTY : "EMPTY" > }
TOKEN : { < PCDATA : "#PCDATA" > }

TOKEN : { < CDATA : "CDATA" > }
TOKEN : { < ID : "ID" > }
TOKEN : { < IDREF : "IDREF" > }

TOKEN : { < FLAG_REQUIRED : "#REQUIRED" > }
TOKEN : { < FLAG_IMPLIED : "#IMPLIED" > }
TOKEN : { < FLAG_DEFAULT : "#DEFAULT" > }
TOKEN : { < FLAG_FIXED : "#FIXED" > }

TOKEN : { < OPEN_PAR : "(" > }
TOKEN : { < CLOSE_PAR : ")" > }
TOKEN : { < QUOTE : "\"" > }
TOKEN : { < COMMA : "," > }
TOKEN : { < PIPE : "|" > }
TOKEN : { < PLUS : "+" > }
TOKEN : { < STAR : "*" > }
TOKEN : { < QUESTION_MARK : "?" > }

TOKEN : { < IDENTIFIER : (["a"-"z", "A"-"Z", "-", "_"])+ > }

SchemaGraph Schema() : { ElementDeclaration elDecl; List<AttributeDeclaration> attDecls; }
{
	{ boolean rootEl = true; }
	{ builder.clear(); }
	
	(
		elDecl = ElDeclaration()
		{ builder.addDeclaration(elDecl, rootEl); }
		{ rootEl = false; }
	|
		attDecls = AttlistDeclaration()
		{ builder.addDeclarations(attDecls); }
	)*
	
	<EOF>
	
	{ return builder.getResult(); }
}

ElementDeclaration ElDeclaration() : { Token t; ElementDeclaration result; }
{
	<EL_DECL_START>
	
	t = <IDENTIFIER>
	{ String elName = t.image; }
	
	result = ElContent(elName)
	
	<DECL_END>
	
	{ return result; }
}

ElementDeclaration ElContent(String elName) : { Expression expr; ElementDeclaration result; }
{
	<OPEN_PAR>
	
	(
		<PCDATA>
		{ result = new ElementDeclaration(elName, new StringContent(), new Sequence()); }
	|
		expr = REGEXP()
		{ result = new ElementDeclaration(elName, new EmptyContent(), expr); }
	)
	
	<CLOSE_PAR>
	
	{ return result; }
}

Expression REGEXP() : { Expression result; Expression subExpr; }
{
	result = TERM()
	
	(
		<PIPE>
		
		subExpr = REGEXP()
		{ result = new Union(result, subExpr); }
	)?
	
	{ return result; }
}

Expression TERM() : { Expression subExpr; }
{
	{ List<Expression> subExprs = new ArrayList<Expression>(); }
	
	(
		subExpr = FACTOR()
		{ subExprs.add(subExpr); }
		
		(
			<COMMA>
			
			subExpr = FACTOR()
			{ subExprs.add(subExpr); }
		)*
	)?
	
	{ return new Sequence(subExprs); }
}

Expression FACTOR() : { Expression result; }
{
	result = BASE()
	
	(
		<PLUS>
		{ result = new Iteration(1, maxCard, result); }
	|
		<STAR>
		{ result = new Iteration(0, maxCard, result); }
	|
		<QUESTION_MARK>
		{ result = new Iteration(0, 1, result); }
	)?
	
	{ return result; }
}

Expression BASE() : { Expression result; Token t; }
{
	(
		t = <IDENTIFIER>
		{ result = new Identifier(t.image); }
	|
		<OPEN_PAR>
		
		result = REGEXP()
		
		<CLOSE_PAR>
	)
	
	{ return result; }
}

List<AttributeDeclaration> AttlistDeclaration() : { Token t; ContentType contentType; AttributeDeclaration attrDecl; }
{
	{ List<AttributeDeclaration> result = new ArrayList<AttributeDeclaration>(); }
	
	<AT_DECL_START>
	
	t = <IDENTIFIER>
	{ String elName = t.image; }
	
	(
		t = <IDENTIFIER>
		{ String attrName = t.image; }
		
		contentType = AttrContent()
		
		attrDecl = AttrDefaultValues(elName, attrName, contentType)
		
		{ result.add(attrDecl); }
	)+
	
	<DECL_END>
	
	{ return result; }
}

ContentType AttrContent() : { Token t; ContentType result; }
{
	(
		<CDATA>
		{ result = new StringContent(); }
	|
		<OPEN_PAR>
		
		{ Set<String> values = new HashSet<String>(); }
		
		t = <IDENTIFIER>
		{ values.add(t.image); }
		
		(
			<PIPE>
			
			t = <IDENTIFIER>
			{ values.add(t.image); }
		)*
		
		{ result = new EnumContent(values); }
		
		<CLOSE_PAR>
	)
	
	{ return result; }
}

AttributeDeclaration AttrDefaultValues(String elName, String attrName, ContentType contentType) : { Token t; boolean required; }
{
	(
		<FLAG_REQUIRED>
		{ required = true; }
	|
		<FLAG_IMPLIED>
		{ required = false; }
	|
		<FLAG_FIXED>
		
		<QUOTE>
		t = <IDENTIFIER>
		<QUOTE>
		
		// TODO: validate that the original content type contains the default value
		{ contentType = new EnumContent(t.image); }
		{ required = false; }
	|
		<QUOTE>
		t = <IDENTIFIER>
		<QUOTE>
		
		{ required = false; }		
	)
	
	{ return new AttributeDeclaration(elName, attrName, contentType, required); }
}
